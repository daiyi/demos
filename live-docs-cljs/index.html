<!DOCTYPE html>
<meta charset="utf-8">
<head>
  <link rel="stylesheet" type="text/css" href="https://storage.googleapis.com/app.klipse.tech/css/codemirror.css">

  <style type="text/css">
    body, html {
      height: 100%;
      margin: 0;
      padding: 0;
    }
    .CodeMirror.cm-s-default {
      /*max-height: 50%;*/
      border: none;
      background: #ddd;
    }
    .CodeMirror-scroll {
      padding: 10px;
    }
    .CodeMirror.cm-s-default + .CodeMirror.cm-s-default {
      display: none;
      /*background: #27e10e*/
    }
    .wrapper {
      height: 100%;
      display: flex;
      align-items: flex-start;
      justify-content: space-between;
    }
    .box {
      flex: 1;
      max-width: 49%;
      height: 100%;
      /*padding: 10px;*/
    }
  </style>
</head>

<body>


  <div class="wrapper">
    <div class="box">
      <pre class="editor">
(ns simple.core
  (:require [reagent.core :as reagent]
            [re-frame.core :as rf]))

;; A detailed walk-through of this source code is provied in the docs:
;; https://github.com/Day8/re-frame/blob/master/docs/CodeWalkthrough.md

;; -- Domino 1 - Event Dispatch -----------------------------------------------

(defn dispatch-timer-event
  []
  (let [now (js/Date.)]
    (rf/dispatch [:timer now])))  ;; <-- dispatch used

;; Call the dispatching function every second.
;; `defonce` is like `def` but it ensures only instance is ever
;; created in the face of figwheel hot-reloading of this file.
(defonce do-timer (js/setInterval dispatch-timer-event 1000))


;; -- Domino 2 - Event Handlers -----------------------------------------------

(rf/reg-event-db              ;; sets up initial application state
  :initialize                 ;; usage:  (dispatch [:initialize])
  (fn [_ _]                   ;; the two parameters are not important here, so use _
    {:time (js/Date.)         ;; What it returns becomes the new application state
     :time-color "#f88"}))    ;; so the application state will initially be a map with two keys


(rf/reg-event-db                ;; usage:  (dispatch [:time-color-change 34562])
  :time-color-change            ;; dispatched when the user enters a new colour into the UI text field
  (fn [db [_ new-color-value]]  ;; -db event handlers given 2 parameters:  current application state and event (a vector)
    (assoc db :time-color new-color-value)))   ;; compute and return the new application state


(rf/reg-event-db                 ;; usage:  (dispatch [:timer a-js-Date])
  :timer                         ;; every second an event of this kind will be dispatched
  (fn [db [_ new-time]]          ;; note how the 2nd parameter is destructured to obtain the data value
    (assoc db :time new-time)))  ;; compute and return the new application state


;; -- Domino 4 - Query  -------------------------------------------------------

(rf/reg-sub
  :time
  (fn [db _]     ;; db is current app state. 2nd unused param is query vector
    (-> db
        :time)))

(rf/reg-sub
  :time-color
  (fn [db _]
    (:time-color db)))


;; -- Domino 5 - View Functions ----------------------------------------------

(defn clock
  []
  [:div.example-clock
   {:style {:color @(rf/subscribe [:time-color])}}
   (-> @(rf/subscribe [:time])
       .toTimeString
       (clojure.string/split " ")
       first)])

(defn color-input
  []
  [:div.color-input
   "Time color: "
   [:input {:type "text"
            :value @(rf/subscribe [:time-color])
            :on-change #(rf/dispatch [:time-color-change (-> % .-target .-value)])}]])  ;; <---

(defn ui
  []
  [:div
   [:h1 "Hello world, it is now"]
   [clock]
   [color-input]])

;; -- Entry Point -------------------------------------------------------------

(defn ^:export run
  []
  (rf/dispatch-sync [:initialize])     ;; puts a value into application state
  (reagent/render [ui]              ;; mount the application's ui into '<div id="app" />'
                  (js/document.getElementById "app")))


(run)
      </pre>
      <!-- <pre class="editor">
        (ns second.core)
        (+ 1 5)
      </pre> -->
    </div>
    <div class="box">
      <div id="app" style="margin-bottom: 30px"></div>
    </div>
  </div>


  <script>
      window.klipse_settings = {
          selector: '.editor', // css selector for the html elements you want to klipsify
          // eval_idle_msec: 2000,
          codemirror_options_in: {
              lineWrapping: true,
              autoCloseBrackets: true
          }
      };
  </script>
  <script src="https://storage.googleapis.com/app.klipse.tech/plugin/js/klipse_plugin.js"></script>
</body>
