<!DOCTYPE html>
<meta charset="utf-8">
<head>
  <link rel="stylesheet" type="text/css" href="https://storage.googleapis.com/app.klipse.tech/css/codemirror.css">
  <link rel="stylesheet" type="text/css" href="main.css">
  <link rel="stylesheet" type="text/css" href="./editor-theme.css">
</head>

<body>
  <div class="page">
    <div class="panel panel-editor">
      <div class="nav">
        <ul>
          <li>
            :<a href="/">re-frame-playground</a>
          </li>
          <li>
            :<a href="https://github.com/Day8/re-frame/blob/master/README.md">docs</a>
          </li>
        </ul>
      </div>
      <div class="editor-wrapper">
        <div id="editor">
          <pre id="editor-contents">
(ns simple.core
(:require [reagent.core :as reagent]
          [re-frame.core :as rf]
          [clojure.string :as str]))

;; A detailed walk-through of this source code is provied in the docs:
;; https://github.com/Day8/re-frame/blob/master/docs/CodeWalkthrough.md

;; -- Domino 1 - Event Dispatch -----------------------------------------------

(defn dispatch-timer-event
  []
  (let [now (js/Date.)]
    (rf/dispatch [:timer now])))  ;; <-- dispatch used

;; Call the dispatching function every second.
;; `defonce` is like `def` but it ensures only one instance is ever
;; created in the face of figwheel hot-reloading of this file.
(defonce do-timer (js/setInterval dispatch-timer-event 1000))


;; -- Domino 2 - Event Handlers -----------------------------------------------

(rf/reg-event-db              ;; sets up initial application state
  :initialize                 ;; usage:  (dispatch [:initialize])
  (fn [_ _]                   ;; the two parameters are not important here, so use _
    {:time (js/Date.)         ;; What it returns becomes the new application state
     :time-color "#f88"}))    ;; so the application state will initially be a map with two keys


(rf/reg-event-db                ;; usage:  (dispatch [:time-color-change 34562])
  :time-color-change            ;; dispatched when the user enters a new colour into the UI text field
  (fn [db [_ new-color-value]]  ;; -db event handlers given 2 parameters:  current application state and event (a vector)
    (assoc db :time-color new-color-value)))   ;; compute and return the new application state


(rf/reg-event-db                 ;; usage:  (dispatch [:timer a-js-Date])
  :timer                         ;; every second an event of this kind will be dispatched
  (fn [db [_ new-time]]          ;; note how the 2nd parameter is destructured to obtain the data value
    (assoc db :time new-time)))  ;; compute and return the new application state


;; -- Domino 4 - Query  -------------------------------------------------------

(rf/reg-sub
  :time
  (fn [db _]     ;; db is current app state. 2nd unused param is query vector
    (:time db))) ;; return a query computation over the application state

(rf/reg-sub
  :time-color
  (fn [db _]
    (:time-color db)))


;; -- Domino 5 - View Functions ----------------------------------------------

(defn clock
  []
  [:div.example-clock
   {:style {:margin-bottom 10 :font-size 25 :color @(rf/subscribe [:time-color])}}
   (-> @(rf/subscribe [:time])
       .toTimeString
       (str/split " ")
       first)])

(defn color-input
  []
  [:div.color-input
   "Time color: "
   [:input {:type "text"
            :value @(rf/subscribe [:time-color])
            :on-change #(rf/dispatch [:time-color-change (-> % .-target .-value)])}]])  ;; <---

(defn ui
  []
  [:div
   [:h1 "re-frame playground"]
   [clock]
   [color-input]

   [:h2 "try some other examples!"]
   [:ul
    [:li
     [:a {:href "?gist-id=sklind/1398f41345ea4df551b0c370ac1ac822"} "hello world clock"]]
    [:li
     [:a {:href "/"} "another example tbd"]]]

   [:h2 "loading samples"]
    [:p "you can load gists with this url param: " [:code "gist-id=[github-username]/[gist-id]"]]
    [:p "for example, "
     [:a {:href "?gist-id=mhuebert/562e88d75c04178f1c18ddacc7cff2b7"} "?gist-id=mhuebert/562e88d75c04178f1c18ddacc7cff2b7"]]
   ])

;; -- Entry Point -------------------------------------------------------------

(defn ^:export run
  []
  (rf/dispatch-sync [:initialize])     ;; puts a value into application state
  (reagent/render [ui]                 ;; mount the application ui into the element with id "app"
                  (js/document.getElementById "app")))

;; -- APPLICATION ENDS --------------------------------------------------------



;; this tells klipse to run the entire application and puts it into the panel to the right!
(run)
          </pre>
        </div>
      </div>
    </div>
    <div class="panel">
        <div id="app">
            <h1>re-frame playground</h1>
            <p>loading...</p>
            <p>(did you remember to <code>(run)</code> your application?)</p>
        </div>
      </div>
  </div>

  <script>
     window.klipse_settings = {
       selector: '#editor-contents', // css selector for the html elements you want to klipsify
       eval_idle_msec: 300,
       codemirror_options_in: {
         autoCloseBrackets: true,
         lineNumbers: true,
         lineWrapping: true,
         theme: 'amy'
       }
     };

    function getParameterByName(name, url) {
      if (!url) url = window.location.href;
      name = name.replace(/[\[\]]/g, "\\$&");
      var regex = new RegExp("[?&]" + name + "(=([^&#]*)|&|#|$)"),
          results = regex.exec(url);
      if (!results) return null;
      if (!results[2]) return '';
      return decodeURIComponent(results[2].replace(/\+/g, " "));
    }

    function loadCode()  {
      var gistId = getParameterByName('gist-id', window.location.href)
      var klipse = document.createElement('script')
      klipse.setAttribute('src', 'https://storage.googleapis.com/app.klipse.tech/plugin/js/klipse_plugin.js')

      if (gistId) {
        var editor = document.getElementById('editor-contents')
        editor.setAttribute('data-gist-id', gistId)
      }

      document.body.appendChild(klipse)
    };

    loadCode();
  </script>
</body>
